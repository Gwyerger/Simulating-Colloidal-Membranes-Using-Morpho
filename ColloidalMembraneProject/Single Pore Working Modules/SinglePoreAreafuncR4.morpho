import meshgen
import meshtools
import plot
import optimize
import moviemaker
import vtk
import functionals


var K = 1
var C = 4
var q = 0
var Hreg = 4
var Lreg = 12
var Rp = 0.75
var gam = 0.5
var dx = 0.2

var pathexport = "/mnt/c/Users/gabey/OneDrive/Desktop/Fall 2023/SM Research/Phase Diagram VTK Exports and PP/Single Pore Round 4 - test pt3/VTK Files/"
var pathenergies = "/mnt/c/Users/gabey/OneDrive/Desktop/Fall 2023/SM Research/Phase Diagram VTK Exports and PP/Single Pore Round 4 - test pt3/Energy over iterations/"


fn modulus(a, b){
    return (a/b - floor(a/b))*b
}

class HalfSpaceLevelSet {
    init(x, M) {
        self.origin = x
        self.direction = M
    }
        // Returns a level set function for this Ellipsoid
    levelset() {
        fn phi (x,y,z) {
            var x0 = self.origin, Mat = self.direction
            return (x.inner(Mat) - x0.inner(Mat))
        }
        return phi
    }
}

class Ellipsoid { // Construct with Ellipsoid(origin, principalradii)
    init(x, r) {
        self.origin = x
        self.principalradii = r
    }
        // Returns a level set function for this Ellipsoid
    levelset() {
        fn phi (x,y,z) {
            var x0 = self.origin, rr = self.principalradii
            return ((x-x0[0])/rr[0])^2 + ((y-x0[1])/rr[1])^2 + ((z-x0[2])/rr[2])^2
            - 1
        }
        return phi
    }
/* Analogous code for gradient() ... */
}

fn CreateAndSaveMesh(Lregion, Hregion, Rpore, Grain, str){
    if (Rpore > 0){
        var hs0 = HalfSpaceDomain(Matrix([0,Hregion]), Matrix([0,1]))
        var hs1 = hs0.difference(HalfSpaceDomain(Matrix([0,Hregion/2]), Matrix([0,-1])))
        var hs2 = hs1.difference(HalfSpaceDomain(Matrix([0,-Hregion/2]), Matrix([0,1])))
        var hs3 = hs2.difference(HalfSpaceDomain(Matrix([Lregion/2,0]), Matrix([-1,0])))
        var rect = hs3.difference(HalfSpaceDomain(Matrix([-Lregion/2,0]), Matrix([1,0])))
        var pore = CircularDomain(Matrix([0,0]), Rpore+0.001)
        var dom = rect.difference(pore)
        var mg = MeshGen(dom, [-Lregion/2..Lregion/2:Grain, -Hregion/2..Hregion/2+0.01:Grain], quiet=true)
        var meshe = mg.build()
        var mesh = ChangeMeshDimension(meshe, 3)
        mesh.addgrade(1)
        mesh.addgrade(2)
        var vtkME = VTKExporter(mesh)
        vtkME.export(String(str,".vtk"))
        print(String("Single pore mesh created and exported as ",str,".vtk"))
        return mesh
    }
    if (Rpore = 0) {
        var hs0 = HalfSpaceDomain(Matrix([0,Hregion]), Matrix([0,1]))
        var hs1 = hs0.difference(HalfSpaceDomain(Matrix([0,Hregion/2]), Matrix([0,-1])))
        var hs2 = hs1.difference(HalfSpaceDomain(Matrix([0,-Hregion/2]), Matrix([0,1])))
        var hs3 = hs2.difference(HalfSpaceDomain(Matrix([Lregion/2,0]), Matrix([-1,0])))
        var rect = hs3.difference(HalfSpaceDomain(Matrix([-Lregion/2,0]), Matrix([1,0])))
        var pore = CircularDomain(Matrix([0,0]), Rpore)
        var dom = rect.difference(pore)
        var mg = MeshGen(dom, [-Lregion/2..Lregion/2:Grain, -Hregion/2..Hregion/2+0.01:Grain], quiet=true)
        var meshe = mg.build()
        var mesh = ChangeMeshDimension(meshe, 3)
        mesh.addgrade(1)
        mesh.addgrade(2)
        var vtkME = VTKExporter(mesh)
        vtkME.export(String(str,".vtk"))
        return mesh
    }
}

fn CreateSelections(mesh, Lregion, Hregion, Rpore) {
    var memb = Selection(mesh, fn(x,y,z) abs(x)<=Lregion/2+dx and abs(y)<=Hregion/2+dx)
    var LeftWallReg = Selection(mesh, fn(x,y,z) x <= -Lregion/2 + dx)
    var RightWallReg = Selection(mesh, fn(x,y,z) x >= Lregion/2 - dx)
    var LowerWallReg = Selection(mesh, fn(x,y,z) y <= -Hregion/2 + dx)
    var UpperWallReg = Selection(mesh, fn(x,y,z) y >= Hregion/2 - dx)
    var bnds = Selection(mesh, boundary=true)
    bnds.addgrade(0)
    LeftWallReg.addgrade(0)
    RightWallReg.addgrade(0)
    LowerWallReg.addgrade(0)
    UpperWallReg.addgrade(0)
    var inner = Selection(mesh, fn(x,y,z) abs(x)<Lregion/2-dx and abs(y)<Hregion/2-dx)
    var lefthalf = Selection(mesh, fn(x,y,z) x<0)
    var righthalf = Selection(mesh, fn(x,y,z) x>0)
    var porebnd = bnds.intersection(inner)
    var porebndleft = porebnd.intersection(lefthalf)
    var porebndright = porebnd.intersection(righthalf)
    var LeftWall = bnds.intersection(LeftWallReg)
    var RightWall = bnds.intersection(RightWallReg)
    var LowerWall = bnds.intersection(LowerWallReg)
    var UpperWall = bnds.intersection(UpperWallReg)
    // Show(plotselection(mesh, RightWall, grade=[0,1]))
    var notporebnd = Selection(mesh, fn(x,y,z) sqrt(x^2 + y^2) > Rpore+0.5*dx)
    porebnd.addgrade(1)
    memb.addgrade(1)
    memb.addgrade(2)
    bnds.addgrade(1)
    LeftWall.addgrade(1)
    RightWall.addgrade(1)
    UpperWall.addgrade(1)
    LowerWall.addgrade(1)
    print("Selections Created: \n memb -> all \n bnds -> all boundarys \n porebnd -> pore boundary \n notporebnd -> region excluded from the pore boundary \n LeftWall -> left side boundary \n RightWall -> right side boundary \n UpperWall -> uppermost boundary \n LowerWall -> lowermost boundary")
    return [memb, LeftWall, RightWall, UpperWall, LowerWall, bnds, porebnd, notporebnd, porebndleft, porebndright]
}

// Helper functions to visualize the director field 
fn visdirector(m, n, scale) {
    var g = Graphics() 
    for (id in 0...m.count()) {
        var x = m.vertexposition(id)        // Get vertex id's position 
        var xx = Matrix([x[0], x[1], 0])    // Promote it to a 3D vector 
        var nn =n[0,id]                     // Get the corresponding director 
        g.display(Cylinder(xx-scale*nn, xx+scale*nn, aspectratio=0.2, color=White)) 
    }
    return g
} 
fn visdirectorselected(m, n, s, scale) {
    var g = Graphics() 
    var points = s.idlistforgrade(0)
    for (id in points) {
        var x = m.vertexposition(id)        // Get vertex id's position 
        var xx = Matrix([x[0], x[1], 0])    // Promote it to a 3D vector 
        var nn =n[0,id]                     // Get the corresponding director 
        g.display(Cylinder(xx-scale*nn, xx+scale*nn, aspectratio=0.2, color=White)) 
    }
    return g
} 

fn visverticalselected(n, nz, s) {
    var points = s.idlistforgrade(0)
    for (id in points) {
        nz[0,id] =n[0,id]         // Get the corresponding director  
    }
    return 
} 

fn Matrix_Morpho2Mathematica(data,str) {
    if (ismatrix(data)){
        print(String("\n",str," = "))
        var size = data.dimensions()
        print("{")
        for (i in 0..size[0]-1){
            if(i>0){print(",")}
            print("{")
            for (j in 0..size[1]-1){
                if(j>0){print(",")}
                print(String(data[i,j]))
                }
            print("}")
            }
        print("}\n")
        }
    return 
}

fn FieldOnLine(m, s, n, start, end, numpts) {
    var N = m.count(0)
    var pts = []
    var ValueMatrix = Matrix(numpts+1,2)
    for (id in 0..N-1) {
        pts.append(m.vertexposition(id))
    }
    var kpts = KDTree(pts)
    var step = (end-start)/numpts
    for (i in 0..numpts) {
        var pt = kpts.nearest(Matrix([i*step + start, 0,0]))
        var nvalue = 0
        var Ptpos = 0
            for (id in 0..N-1) {
                if (pts[id] == pt.location && s.isselected(0, id)) {
                    nvalue = n[0,id].inner(Matrix([0,0,1]))
                    Ptpos = pts[id].inner(Matrix([1,0,0]))
                    break
                }
            }
        ValueMatrix[i,0] = (Ptpos); ValueMatrix[i,1] =  acos(nvalue)
    }
    return ValueMatrix
}

fn EnergiesToFile(str, sopt, fopt) {
    var sonum = Matrix(sopt.energy).dimensions()[0]-1
    var fonum = Matrix(fopt.energy).dimensions()[0]-1
    var fs = File("${pathenergies}${str}sopt.txt", "w")
    for (i in 0..sonum) fs.write("${sopt.energy[i]}")
    var ff = File("${pathenergies}${str}fopt.txt", "w")
    for (i in 0..fonum) ff.write("${fopt.energy[i]}")
    fs.close()
    ff.close()
}

fn ListToFile(str, list){
    var f = File("${pathexport}${str}.txt", "w")
    for (dat in list) f.write(dat)
    f.close()
}


 // //Initialize a field
fn AddNematicFieldEnergy(mesh, problem, sel, Lregion, Hregion, Ksplay, Ktwist, Kbend, q) {
    var nn = Field(mesh, fn(x,y,z) Matrix([0,sin(Pi/2 + Pi/2*arctan(x*sqrt(C/K))/arctan(Lregion/2*sqrt(C/K))),cos(Pi/2 + Pi/2*arctan(x*sqrt(C/K))/arctan(Lregion/2*sqrt(C/K)))])) 
    var nem = Nematic(nn, ksplay = Ksplay, ktwist = Ktwist, kbend = Kbend, pitch = q)
    problem.addenergy(nem, selection = sel)
    return [nn, nem]
}

// For restricting the X-component locally
class XCompSquared {
    init(vec) {
        self.vec = vec
    }
    integrand(vec) {
        var int = vec.op(fn (x) x[0]^2)
        return int.linearize().transpose()
    }

    fieldgradient(vec, mesh) {
        // print vec
        var fg = vec.op(fn (x) Matrix([2*x[0], 0, 0]))
        return fg
    }

    total(vec) {
        return self.integrand(vec).sum()
    }

}

// Add a tilt energy functional C/2*sin^2(theta)
fn tIntegrand(x, n) {                   
    return C/2*(1-(n[2])^2)
}


fn AddTiltEnergy(problem, sel, nn) {
    var tilt = AreaIntegral(tIntegrand, nn) 
    problem.addenergy(tilt, selection = sel)
}

// Add a line tension functional
fn lIntegrand(x){
    return (1)
}
fn AddEdgeEnergy(problem, sel, g) {
    var lten = LineIntegral(lIntegrand)
    problem.addenergy(lten, selection=sel, prefactor = g)
}

fn AddPressure(problem, f) {
    problem.addenergy(Area(), prefactor=(-f))
}

//Add Constraints and Boundary Conditions

fn Bound(x, n) {
    return (1-(n[2])^2)
}

var lcons = ScalarPotential(fn (x,y,z) z)    

// var hsr = HalfSpaceLevelSet(0, Matrix([1,0,0]))
// var hsl = HalfSpaceLevelSet(0, Matrix([-1,0,0]))
// var hsu = HalfSpaceLevelSet(Hreg/2, Matrix([0,1,0]))
// var hsd = HalfSpaceLevelSet(-Hreg/2, Matrix([0,-1,0]))

// var hplus = ScalarPotential(hsr.levelset())
// var hminus = ScalarPotential(hsl.levelset())
// var vplus = ScalarPotential(hsu.levelset())
// var vminus = ScalarPotential(hsd.levelset())


fn AddConstraints(problem, UpperWall, LowerWall, LeftWall, RightWall, porebndleft, porebndright, nn) {
    // Add a level set to keep things in the z axis
    problem.addlocalconstraint(lcons)

    // Add half space constraints to keep the pore from turning inside out (not working yet)
    // problem.addlocalconstraint(hplus, onesided=true)
    // problem.addlocalconstraint(hminus, onesided=true)
    // problem.addlocalconstraint(vplus, onesided=true)
    // problem.addlocalconstraint(vminus, onesided=true)


    // Fully restrict nx
    var lxcs=XCompSquared(nn)
    problem.addlocalconstraint(lxcs, field=nn, target=0)


    // Add a functional to penalize nx
    // var xcon = LineIntegral(fn (x, n) n[0]^2, nn)
    // problem.addenergy(xcon, selection = UpperWall, prefactor = 100)
    // problem.addenergy(xcon, selection = LowerWall, prefactor = 100)

    // // Add a boundary condition
    // var bndry = LineIntegral(Bound, nn)
    // problem.addenergy(bndry, selection=LeftWall)
    // problem.addenergy(bndry, selection=RightWall)

    // Unit vector constraint 
    var lnorm = NormSq(nn)                      
    problem.addlocalconstraint(lnorm, field=nn, target=1) 
}

fn CreateOptimizers(problem, mesh, memb, porebnd, LeftWall, RightWall, UpperWall, LowerWall, nn) {

    // Create the field optimizer
    var fopt = FieldOptimizer(problem, nn)
    fopt.stepsize = 0.01
    fopt.fix(RightWall)
    fopt.fix(LeftWall)
    fopt.quiet = true
    fopt.etol = 1e-5
    var sopt = ShapeOptimizer(problem, mesh)   

    sopt.quiet = true
    sopt.stepsize = 0.001
    sopt.steplimit = 0.01
    sopt.etol = 1e-5
    sopt.fix(RightWall)
    sopt.fix(LeftWall)
    sopt.fix(UpperWall)
    sopt.fix(LowerWall)
    // Part 5: Regularization 
    var reg=OptimizationProblem(mesh)           // Create the Auxiliary regularization problem

    // Add a constraint on the  edge of the boundary to keep the shape of the regularized region a rectangle: We must use a separate problem to fix the shape of the region iteratively

    var leq = EquiElement()                     // Try to equalize elements
    reg.addenergy(leq, prefactor = 1)
    reg.addlocalconstraint(lcons)               // Also respect the level set constraint

    var ropt = ShapeOptimizer(reg, mesh)
    ropt.stepsize = 0.001
    ropt.steplimit = 0.005
    ropt.fix(porebnd)
    ropt.fix(RightWall)
    ropt.fix(LeftWall)
    ropt.fix(UpperWall)
    ropt.fix(LowerWall)
    ropt.quiet=true

    // Not being used
    var bopt = ShapeOptimizer(reg, mesh)
    bopt.stepsize = 0.01
    bopt.steplimit = 0.1
    bopt.quiet = true

    return [fopt, sopt, ropt, bopt, reg]
}

fn Optimization(str, mesh, nn, porebnd, ropt, bopt, fopt, sopt) {

    print(String("Optimizing mesh and exporting with batch name ", str))
    var iterations = 500
    var Break = false
    for (i in 0..iterations) {
        if (i < 200){
            try{
            fopt.conjugategradient(3)
            sopt.conjugategradient(3)
            } catch{"MtrxSnglr": Break = true}
            if (Break) break
            if (Int(modulus(i, 3))==0) {
                ropt.conjugategradient(3)
                if (i < 100) {
                    equiangulate(mesh, quiet=true)
                    ropt.conjugategradient(3)
                }
            }
            if (Int(modulus(i, 5))==0) {
                print("${i}/${iterations}")
            }
        }        
        else {
            try{
            fopt.linesearch(5)
            sopt.linesearch(5)
            } catch{"MtrxSnglr": Break = true}
            if (Break) break
            if (sopt.hasconverged() && fopt.hasconverged()) {
                break
            }
            if (Int(modulus(i, 5))==0) {
                print("${i}/${iterations}")
            }
        }
        var sonum = Matrix(sopt.energy).dimensions()[0]-1
        var fonum = Matrix(fopt.energy).dimensions()[0]-1
        var sEnergy0 = sopt.energy[0]
        var fEnergy0 = fopt.energy[0]
        var sEnergyi = sopt.energy[sonum]
        var fEnergyi = fopt.energy[fonum]
        if(abs(sEnergyi) > abs(sEnergy0*100) or abs(fEnergyi) > abs(fEnergy0*100)) {
            print("Mesh Degradation")
            break
        }
        print("sopt step: ${sEnergyi-sopt.energy[sonum-1]}, fopt step: ${fEnergyi-fopt.energy[fonum-1]}")
    }
}


fn Refinement(str, mesh, nn, memb, porebnd, LeftWall, RightWall, UpperWall, LowerWall, problem, reg, sopt, fopt, ropt, bopt, Nlevels) {
    var vtkE = VTKExporter(nn)
    /* Refinement */
    for (level in 1..Nlevels) {
        var mr = MeshRefiner([mesh, nn, memb, porebnd, LeftWall, RightWall, UpperWall, LowerWall])
        var refmap = mr.refine()

        // Now refinement is done update the problems and optimizers
        for (el in [problem, reg, sopt, fopt, ropt, bopt]) el.update(refmap)

        // Update our references 
        mesh = refmap[mesh]   // There are tidier ways to do this!
        nn = refmap[nn]
        porebnd = refmap[porebnd]
        memb = refmap[memb]
        LeftWall = refmap[LeftWall]
        RightWall = refmap[RightWall]
        UpperWall = refmap[UpperWall]
        LowerWall = refmap[LowerWall]
        // Continue optimizing. We don't need further regularization for this particular problem
        for (i in 1..5) {                  
            sopt.conjugategradient(10)
            fopt.conjugategradient(10)
            if (sopt.hasconverged() && fopt.hasconverged()) break 
        }
        vtkE.export(String(str, "Refined",level,".vtk"))
    }
}



class Visualizer {
    init(mesh) {
        self.mesh = mesh
    }
    showXcomp(nn, sel){
        var nx = Field(self.mesh, fn(x,y,z) Matrix([0,0,0])) // extract x component, plot as density
        visverticalselected(nn, nx, sel)
        nx = nx.op(fn (n) abs(n[0]))
        Show(plotfield(nx, style="interpolate")) 
    }

    showYcomp(nn, sel){
        var ny = Field(self.mesh, fn(x,y,z) Matrix([0,0,0])) // extract x component, plot as density 
        visverticalselected(nn, ny, sel)
        ny = ny.op(fn (n) abs(n[1]))
        Show(plotfield(ny, style="interpolate")) 
    }

    showZcomp(nn, sel){
        var nz = Field(self.mesh, fn(x,y,z) Matrix([0,0,0])) // extract z component, plot as density
        visverticalselected(nn, nz, sel)
        nz = nz.op(fn (n) abs(n[2]))
        Show(plotfield(nz, style="interpolate")) 
    }

    showRods(nn){
        Show(plotmesh(self.mesh, grade = [0,1])+visdirector(self.mesh, nn, 0.05) )
    }
    showEnergyDensity(nem){
        var NemEnDen = nem.integrand(self.mesh)
        var NemEnDenField = Field(self.mesh,grade = 2)
        for (i in 0...self.mesh.count(2)) {
            NemEnDenField[2,i] = NemEnDen[0,i]
        }
        Show(plotfield(NemEnDenField))
    }
}


// Perform simulations across multiple qs and line tensions

var K = 1
// var Ksplay = 1
// var Kbend = 1
// var Ktwist = 1
var C = 4
var q = 0
var Rp = 0.75
var gam = 0.5
var dx = 0.2
var Hreg = 4
var Lreg = 12

CreateAndSaveMesh(Lreg, Hreg, Rp, dx, String("PorousMeshInitial"))

for(i in 0..2){
    var gam = 0.5 + i*0.1

    for(j in 0..2){
    var ff = 0.6 + j*0.1

        for(k in 0..10){
            q =  1 + k*0.1

            var vtkMI = VTKImporter(String("PorousMeshInitial.vtk"))
            var mesh = vtkMI.mesh()
            var Selections = CreateSelections(mesh, Lreg, Hreg, Rp)

            var memb = Selections[0] 
            var LeftWall = Selections[1]
            var RightWall = Selections[2]
            var UpperWall = Selections[3]
            var LowerWall = Selections[4]
            var bnds = Selections[5]
            var porebnd = Selections[6]
            var notporebnd = Selections[7]
            var porebndleft = Selections[8]
            var porebndright = Selections[9]

            // Show(plotselection(mesh, porebnd, grade = [0,1]))
            // Show(plotmesh(mesh, grade = [0,1,2]))
            // Show(plotselection(mesh, RightWall, grade = [0,1]))
            // Show(plotselection(mesh, LeftWall, grade = [0,1]))


            var problem=OptimizationProblem(mesh)

            var fe = AddNematicFieldEnergy(mesh, problem, memb, Lreg, Hreg, K, K, K, q)

            var nn = fe[0]
            var nem = fe[1]

            AddTiltEnergy(problem, memb, nn)

            AddEdgeEnergy(problem, porebnd, gam)

            AddPressure(problem, ff)

            AddConstraints(problem, UpperWall, LowerWall, LeftWall, RightWall, porebndleft, porebndright, nn)

            var Optimizers = CreateOptimizers(problem, mesh, memb, porebnd, LeftWall, RightWall, UpperWall, LowerWall, nn)
            var fopt = Optimizers[0]    
            var sopt = Optimizers[1]
            var ropt = Optimizers[2]
            var bopt = Optimizers[3]
            var reg = Optimizers[4]

            var str = "SinglePoreg${gam}f${ff}q${q}"
            Optimization(str, mesh, nn, porebnd, ropt, bopt, fopt, sopt)
            
            // Show(plotmesh(mesh, grade = [0,1,2]))
            var vtkE = VTKExporter(nn)
            vtkE.export("${pathexport}${str}Final.vtk")

            EnergiesToFile("EnergiesItg${gam}f${ff}q${q}", sopt, fopt)
        }
    }
}