// Here I try to model the expulsion of twist in a colloidal membrane 
// where the area is fixed and the free energy functionals include:
//   K( (∇.n)^2 + (n.∇xn - q)^2 + (nx∇xn)^2 ) + C( 1-(n.z)^2 )

import meshgen
import meshtools
import kdtree
import plot
import optimize
import moviemaker

var K = 1
var A = 100
var q = 4
var gpar = 0
var gper = 0


// Defining a circular domain (Isotropic Fluid) with a smaller circular selection space (Colloidal Membrane): Units of length are in 10^-5 m, or 10s of microns
var Wreg = 6
var Hreg = 4
var Wmem = 6
var Hmem = 4

var mesh = AreaMesh(fn (u,v) [u,v,0], -Wreg..Wreg:0.2, -Hreg..Hreg:0.2)

var col = Selection(mesh, fn(x,y,z) abs(x) <= Wmem + 0.01 && abs(y) <= Hmem + 0.01 && abs(x + y) < 2*Wmem - 0.01)
var bnd = Selection(mesh, fn(x,y,z) abs(x) <= Wmem + 0.01 && abs(y) <= Hmem + 0.01 && (abs(x) >= Wmem - 0.01 || abs(y) >= Hmem - 0.01) && abs(x + y) < 2*Wmem - 0.01)
var leftwall = Selection(mesh, fn(x,y,z) x <= -(Wmem - 0.01) )
var rightwall = Selection(mesh, fn (x,y,z) x >= (Wmem - 0.01) )
mesh.addgrade(1)
col.addgrade(0)
col.addgrade(1)
col.addgrade(2)

bnd.addgrade(0)
bnd.addgrade(1)

leftwall.addgrade(0)
leftwall.addgrade(1)

rightwall.addgrade(0)
rightwall.addgrade(1)


//Initialize a random field
// var nn = Field(mesh, fn(x,y,z) Matrix([0,sin(q*x),cos(q*x)]))  
var nn = Field(mesh, fn(x,y,z) Matrix([random(),random(),random()]))  


// Helper functions to visualize the director field 
fn visdirector(m, n, scale) {
    var g = Graphics() 
    for (id in 0...m.count()) {
        var x = m.vertexposition(id)        // Get vertex id's position 
        var xx = Matrix([x[0], x[1], 0])    // Promote it to a 3D vector 
        var nn =n[0,id]                     // Get the corresponding director 
        g.display(Cylinder(xx-scale*nn, xx+scale*nn, aspectratio=0.2, color=White)) 
    }
    return g
} 
fn visdirectorselected(m, n, s, scale) {
    var g = Graphics() 
    var points = s.idlistforgrade(0)
    for (id in points) {
        var x = m.vertexposition(id)        // Get vertex id's position 
        var xx = Matrix([x[0], x[1], 0])    // Promote it to a 3D vector 
        var nn =n[0,id]                     // Get the corresponding director 
        g.display(Cylinder(xx-scale*nn, xx+scale*nn, aspectratio=0.2, color=White)) 
             
    }
    return g
} 

fn Matrix_Morpho2Mathematica(data,str) {
    if (ismatrix(data)){
        print(String("\n",str," = "))
        var size = data.dimensions()
        print("{")
        for (i in 0..size[0]-1){
            if(i>0){print(",")}
            print("{")
            for (j in 0..size[1]-1){
                if(j>0){print(",")}
                print(String(data[i,j]))
                }
            print("}")
            }
        print("}\n")
        }
    return 
}

 // Create an OptimizationProblem with our mesh as the target
var problem=OptimizationProblem(mesh) 

 // Add an Area functional as a global constraint
var larea = Area()                                         
problem.addconstraint(larea, selection = col) 

// Add a nematic energy functional 
var nem = Nematic(nn, ksplay = K, ktwist = K, kbend = K, pitch = q)
problem.addenergy(nem, selection = col) 

// Add a level set to keep things in the z axis
var lcons = ScalarPotential(fn (x,y,z) 100*z)                     
problem.addlocalconstraint(lcons, selection=col) 

fn lbndintegrand(x, n) {
    return 1-(n[2])^2
}

var lbnd = LineIntegral(lbndintegrand, nn)
problem.addenergy(lbnd, selection= leftwall, prefactor = A)


/*
// Add a line tension functional
fn lIntegrand(x, n) {
    var t = tangent()
    var lp = (t.inner(n))^2
    var lt  = 1-lp
    return (gpar*lp + gper*lt)
}
var lten = LineIntegral(lIntegrand, nn)

problem.addenergy(lten, selection=bnd)
*/

// Unit vector constraint 
var lnorm = NormSq(nn)                      
problem.addlocalconstraint(lnorm, field=nn, target=1) 


// Create the field optimizer
var fopt = FieldOptimizer(problem, nn)
fopt.stepsize = 0.01
// Create the shape optimizer
var sopt = ShapeOptimizer(problem, mesh)   
sopt.stepsize = 0.01
/* Part 5: Regularization */
var reg=OptimizationProblem(mesh)           // Create the Auxiliary regularization problem

var leq = EquiElement()                     // Try to equalize elements
reg.addenergy(leq)
reg.addlocalconstraint(lcons, selection=col) // Also respect the level set constraint

var ropt = ShapeOptimizer(reg, mesh)        // We use a separate regularization optimizer
ropt.stepsize = 0.001
ropt.fix(bnd)


for (i in 0...100) {
    print(String("field optimizer ",i))
    fopt.relax(15)
}

var Nlevels = 0
/* Part 6: Refinement */
for (level in 1..Nlevels) {
    var mr = MeshRefiner([mesh, nn, col, bnd, leftwall])
    var refmap = mr.refine()

    // Now refinement is done update the problems and optimizers
    for (el in [problem, reg, sopt, fopt, ropt]) el.update(refmap)

    // Update our references 
    mesh = refmap[mesh]   // There are tidier ways to do this!
    nn = refmap[nn]
    bnd = refmap[bnd]
    col = refmap[col]
    leftwall = refmap[leftwall]

    // Continue optimizing. We don't need further regularization for this particular problem
    for (i in 1..10) {                  
        fopt.conjugategradient(10)
        if (fopt.hasconverged()) break 
    }
}

col.addgrade(1)
// Visualize 
Show(plotselection(mesh, col, grade = 1)+visdirectorselected(mesh, nn, col, 0.05))

var nvert = Field(mesh, fn(x,y,z) Matrix([0,0,0])) // extract vertical component, plot as density

fn visverticalselected(n, nz, s) {
    var points = s.idlistforgrade(0)
    for (id in points) {
        nz[0,id] =n[0,id]         // Get the corresponding director  
    }
    return 
} 

visverticalselected(nn, nvert, col)
nvert = nvert.op(fn (n) abs(n[0])^2+abs(n[1])^2)

fn FindOuterXBound(m, s) {
    var points = s.idlistforgrade(0)
    var i = 0
    var sct = s.count(0)
    var x = Matrix(sct,1)
    for (id in points) {
        x[i] = m.vertexposition(id)[0]
        i=i+1
    }
    return max(x)
}

fn GeneralScaleBar() {
    var len = Pi/q
    var X = 0
    var Y = -1
    var g = Graphics()
    g.display(Cylinder(Matrix([X,Y,0]), Matrix([X+len,Y,0]), aspectratio = 0.2))
    return g
}

fn FieldOnLine(m, s, n, start, end, numpts) {
    var N = m.count(0)
    var pts = []
    var ValueMatrix = Matrix(numpts+1,2)
    for (id in 0..N-1) {
        pts.append(m.vertexposition(id))
    }
    var kpts = KDTree(pts)
    var step = (end-start)/numpts
    for (i in 0..numpts) {
        var pt = kpts.nearest(Matrix([i*step + start, 0,0]))
        var nzvalue = 0
        var Ptpos = 0
            for (id in 0..N-1) {
                if (pts[id] == pt.location && s.isselected(0, id)) {
                    nzvalue = n[0,id].inner(Matrix([0,0,1]))
                    Ptpos = pts[id].inner(Matrix([1,0,0]))
                    break
                }
            }
        ValueMatrix[i,0] = Ptpos; ValueMatrix[i,1] =  acos(nzvalue)
    }
    return ValueMatrix
}


Show(plotfield(nvert, style="interpolate", cmin = 0, cmax = 1)+GeneralScaleBar()) 